#!/bin/csh
# Correctness: size(-p WINDOW) << size(no flags)?
set TESTS = /c/cs323/Hwk2/Tests
set RUN   = /c/cs323/bin/run

set EFLAG = "-p 8000"
set XFLAG =
set FILE  = /c/cs323/Style/C-DEC
set FUDGE = 93

set EOUT  = /tmp/lzw.e.$$
set DOUT  = /tmp/lzw.d.$$
set XOUT  = /tmp/lzw.x.$$

onintr cleanup

$RUN ./encode $EFLAG < $FILE >! $EOUT
$RUN ./decode        < $EOUT >! $DOUT
/usr/bin/cmp - $FILE < $DOUT

$RUN ./encode $XFLAG < $FILE >! $XOUT
$RUN ./decode        < $XOUT >! $DOUT
/usr/bin/cmp - $FILE < $DOUT

set ESIZE = `wc -c < $EOUT`
set XSIZE = `wc -c < $XOUT`

@ BOUND = ($FUDGE * $XSIZE) / 100
if ($ESIZE > $BOUND) then
  echo "size($EFLAG) = $ESIZE > $BOUND = .$FUDGE*$XSIZE = .$FUDGE*size(no flags)"
endif

if (${?DEBUG_SCRIPT}) then
  echo "size($EFLAG) = $ESIZE > $BOUND = .$FUDGE*$XSIZE = .$FUDGE*size(no flags)"
endif

cleanup:
/bin/rm -f $EOUT $DOUT $XOUT
