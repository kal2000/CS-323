#!/bin/csh
# Correctness: size(-p WINDOW -m MAXBITS) << size(-m MAXBITS)?
set TESTS = /c/cs323/Hwk2/Tests
set RUN   = /c/cs323/bin/run

set EFLAG = "-p 4000 -m 11"
set XFLAG = "        -m 11"
set FILE  = /bin/cat
set FUDGE = 90

set EOUT  = /tmp/lzw.e.$$
set DOUT  = /tmp/lzw.d.$$
set XOUT  = /tmp/lzw.x.$$

onintr cleanup

$RUN ./encode $EFLAG < $FILE >! $EOUT
$RUN ./decode        < $EOUT >! $DOUT
/usr/bin/cmp - $FILE < $DOUT

$RUN ./encode $XFLAG < $FILE >! $XOUT
$RUN ./decode        < $XOUT >! $DOUT
/usr/bin/cmp - $FILE < $DOUT

set ESIZE = `wc -c < $EOUT`
set XSIZE = `wc -c < $XOUT`

@ BOUND = ($FUDGE * $XSIZE) / 100
if ($ESIZE > $BOUND) then
  echo "size($EFLAG) = $ESIZE > $BOUND = .$FUDGE * $XSIZE = .$FUDGE * size($XFLAG)"
endif

if (${?DEBUG_SCRIPT}) then
  echo "size($EFLAG) = $ESIZE : $BOUND = .$FUDGE * $XSIZE = .$FUDGE * size($XFLAG)"
endif

cleanup:
/bin/rm -f $EOUT $DOUT $XOUT
